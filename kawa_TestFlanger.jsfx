//====================================================================
desc:kawa TestFlanger
//====================================================================

// imports
//====================================================================
import kawa_ALL.jsfx-inc
//====================================================================

// sliders
//--------------------------------------------------------------------
slider1:0.1<0.0,1.0>RATE 
slider2:0.6<0.0,1.0>WIDTH
slider3:0.0<0,1,{FLANGER,PHASER}>MODE
//====================================================================

@init
//====================================================================

    // create APP ID
    //================================================================
    APP_ID = APP_create();
    //================================================================

    //create Layer ID
    //================================================================
    LAYER_001 = LAYER_create("LAYER01");
    //================================================================

    //create Component and add to layer 1 .
    //================================================================
    TEST_comp  = COMPONENT_create("TEST");
    XYSlider_1 = XYSLIDER_create("XYSlider_1");
    PHASER_ModeButton_comp   = BUTTON_create("FLANGER");
    BUTTON_setOnTextPostFix (PHASER_ModeButton_comp,"");
    BUTTON_setOffTextPostFix(PHASER_ModeButton_comp,"");
    COMPONENT_changeColor( PHASER_ModeButton_comp
                      ,"butonOnBackGroundColor"
                      ,1,1,0,1);
    //================================================================
    COMPONENT_setSliderIndex(PHASER_ModeButton_comp,3);
    COMPONENT_changeColor(PHASER_ModeButton_comp,"textColor_mouseon",0,0,0,1);
    //----------------------------------------------------------------
    LAYER_addChildComponent(LAYER_001 , TEST_comp);
    LAYER_addChildComponent(LAYER_001 , PHASER_ModeButton_comp);
    LAYER_addChildComponent(LAYER_001 , XYSlider_1);
    //================================================================

    // layer add to app
    //================================================================
    APP_addChildComponent(APP_ID,LAYER_001);
    //================================================================
    LAYER_setActive(LAYER_001,true);
    //================================================================

    // canvase test
    //================================================================
    CANVS_WAVE_L = CANVAS_create(gfx_w,gfx_h);
    CANVS_WAVE_R = CANVAS_create(gfx_w,gfx_h);
    //================================================================
    
    delaySize = srate;
    DELAY_ARRAY_L = CURSOLBUFFER_create(delaySize);
    DELAY_ARRAY_R = CURSOLBUFFER_create(delaySize);
    //================================================================
    
    //================================================================
    DBG_toggle(DBG); // visible off
    //================================================================
    
    // for particle 
    PARTICLE_controller = PARTICLE_CONTROLLER_create();
    //================================================================

@slider
//====================================================================
    //XYSLIDER_setYValue(XYSlider_1,slider1,false);
    //XYSLIDER_setXValue(XYSlider_1,slider2,false);
    COMPONENT_setValue(PHASER_ModeButton_comp,slider3,faluse);
    
@block
//====================================================================
    max_L = 0;min_L = 0;
    max_R = 0;min_R = 0;
    //================================================================
    
@sample
//====================================================================
    max_L = max(max_L,spl0);
    min_L = min(min_L,spl0);
    max_R = max(max_R,spl1);
    min_R = min(min_R,spl1);
    //================================================================
 
    //================================================================
    (BUTTON_isON(PHASER_ModeButton_comp) ==true)? //phaser mode
    (
        //============================================================
        rateX = slider1 * 0.60 + 0.001;
        valueX = 0.5 + sin( (2*$pi* rateX * timeX )/srate)/2;
        timeX +=1;(timeX>=srate/rateX)?( timeX=0;);
        //============================================================
        rateY = slider2 * 0.03 + 0.001;
        valueY = 0.5 + sin( (2*$pi* rateY * timeY)/srate)/2;
        timeY +=1;(timeY>=srate/rateY)?( timeY=0;);
        //============================================================
        
        //============================================================
        CURSOLBUFFER_push( DELAY_ARRAY_L,spl0 -delay_L*0.6 );    
        CURSOLBUFFER_push( DELAY_ARRAY_R,spl1 -delay_R*0.6 );  
        
        //============================================================
        XYSLIDER_setXValue(XYSlider_1,valueX,false);
        XYSLIDER_setYValue(XYSlider_1,valueY,false);
        
        chorusWidth = srate* ( 0.001  );
        //============================================================
        delay_L = CURSOLBUFFER_getOffsettedValue( DELAY_ARRAY_L
                                                , -srate/rateX * 0.000001 * XYSLIDER_getYValue(XYSlider_1)
                                                  -chorusWidth * XYSLIDER_getXValue(XYSlider_1) );
        delay_R = CURSOLBUFFER_getOffsettedValue( DELAY_ARRAY_R
                                                , -srate/rateY * 0.000050  * XYSLIDER_getXValue(XYSlider_1)
                                                  -chorusWidth * XYSLIDER_getXValue(XYSlider_1) );
        //============================================================
        
    ):( // FLANGER MODE
        //============================================================
        rateX= slider1;
        valueX = 0.5 + sin( (2*$pi* rateX * timeX)/srate)/2 ;
        timeX +=1;(timeX>=srate/rateX)?( timeX=0;);
        //============================================================
        rateY = slider2;
        valueY = 0.55 + sin( (2*$pi* rateY * timeY)/srate)/2*0.9;
        timeY +=1;(timeY>=srate/rateY)?( timeY=0;);
        //============================================================
        
        //============================================================
        CURSOLBUFFER_push( DELAY_ARRAY_L,spl0 );    
        CURSOLBUFFER_push( DELAY_ARRAY_R,spl1 );  
        
        //============================================================
        XYSLIDER_setXValue(XYSlider_1,valueX,false);
        XYSLIDER_setYValue(XYSlider_1,valueY,false);
        
        chorusWidth = srate* ( 0.002 * XYSLIDER_getYValue(XYSlider_1) );
        //============================================================
        delay_L = CURSOLBUFFER_getOffsettedValue( DELAY_ARRAY_L
                                                , -chorusWidth * XYSLIDER_getXValue(XYSlider_1) );
        delay_R = CURSOLBUFFER_getOffsettedValue( DELAY_ARRAY_R
                                                , -chorusWidth * XYSLIDER_getXValue(XYSlider_1) );
        //============================================================
    );
    //================================================================
    spl0 = spl0 + delay_L;
    spl1 = spl1 + delay_R;
    //================================================================

@gfx 200 400
//====================================================================
    // direct draw background to
    //================================================================
    drawBackGrond_color(5,0,60);
    drawSinWaveCircle(100,0.8,1,0.0043);
    //================================================================

    // canvase test
    //================================================================
    drawWaveBufferToCanvas(CANVS_WAVE_L
                          ,max_L
                          ,min_L
                          ,5
                          ,0.3
                          ,1
                          ,80);

    drawWaveBufferToCanvas(CANVS_WAVE_R
                          ,max_R
                          ,min_R
                          ,5
                          ,0.4
                          ,1
                          ,0);
    //================================================================
    gfx_a =0.6;
    CANVAS_blitImage4(CANVS_WAVE_R,1,0);
    CANVAS_blitImage4(CANVS_WAVE_L,1,0);
    gfx_a =1;
    //================================================================
    // final background
    //================================================================
    drawDotGrid(16,16,0.6);
    //================================================================
   
    // for debug
    //================================================================
    DBGMESSAGE_drawAllMessage(DBG);
    //================================================================
    
    // for particle 
    PARTICLE_CONTROLLER_update ( PARTICLE_controller );
    //================================================================
    ( APP_isMouseButtonHoldingPress(APP_ID)  ==true) ?      
    //( APP_isMouseButtonPressed(APP_ID)  ==true) ?
    //(true)?
    (
        PARTICLE_CONTROLLER_addParticleType1(PARTICLE_controller);
        0;
    );
    //================================================================
    PARTICLE_CONTROLLER_addParticleType3_XY (PARTICLE_controller
                                            ,XYSLIDER_getCircleAreaCX(XYSlider_1)
                                            ,XYSLIDER_getCircleAreaCY(XYSlider_1)
                                            ,false
                                            );
    //================================================================

    // App update. check and compare with last event state.(resize or mouse)
    //================================================================
    APP_update(APP_ID);
    
    //================================================================
    ( mouse_cap & 64 ==64 && lastCAP != mouse_cap )?
    (
        DBG_toggle(DBG);
    );
    lastCAP = mouse_cap;
    //================================================================
    ( APP_isMouseButtonPressed(APP_ID)  ==true) ?
    ( 0;);
    
    ( APP_isMouseButtonReleased(APP_ID) ==true) ?
    ( 0;);
    //================================================================
    
    
    // alignment position when resized.
    //================================================================
    ( APP_isWindowResized(APP_ID) ==true) ?
    (
        RECTANGLE_copyTo      ( CANVS_WAVE_L,APP_ID);
        RECTANGLE_copyTo      ( CANVS_WAVE_R,APP_ID);
        CANVAS_recreateCanvas ( CANVS_WAVE_L ); 
        CANVAS_recreateCanvas ( CANVS_WAVE_R );
        //============================================================
        RECTANGLE_copyTo      ( LAYER_001,APP_ID);  // layer resize 
        //============================================================
    );
    //================================================================

    //================================================================
    ( LAYER_isAreaResized(LAYER_001) ==true)   ?
    (
        RECTANGLE_copyTo         (TEST_comp,LAYER_001);
        RECTANGLE_cropFromTopP   (TEST_comp,0.025);
        //============================================================
        RECTANGLE_copyTo         (XYSlider_1,LAYER_001);
        RECTANGLE_reduceP        (XYSlider_1,0.1,0.125);
        //============================================================
        RECTANGLE_copyTo         (PHASER_ModeButton_comp,LAYER_001);
        RECTANGLE_cropFromTopP   (PHASER_ModeButton_comp,0.06);
        //RECTANGLE_cropFromRightP   (PHASER_ModeButton_comp,0.3);
    );
    //================================================================
    
    (BUTTON_isON(PHASER_ModeButton_comp)==true)?
    (
        COMPONENT_setName(PHASER_ModeButton_comp,"PHASER MODE ");
    ):
    (
        COMPONENT_setName(PHASER_ModeButton_comp,"FLANGER MODE");
    );
    //================================================================
    
    //================================================================
    // if animation
    //================================================================
    COMPONENT_setComponentAlpha(TEST_comp,abs(sin($pi*2*mod*2)));
    mod += 0.01;(mod > 1 )?(mod=0;);
    //================================================================
    //RECTANGLE_setX( LAYER_001,40*sin($pi*2*mod));
    
    //================================================================
    // plugin Title Str
    //================================================================
    RECTANGLE_drawBottomRightText( APP_ID,"kawa Test Flanger");
    //================================================================

//====================================================================
// ç¶š

