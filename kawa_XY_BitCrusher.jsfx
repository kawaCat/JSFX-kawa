//====================================================================
desc:_kawa_XY_BitCrusher
//====================================================================

// imports
//====================================================================
import kawa_ALL.jsfx-inc
//====================================================================

// sliders //( this app lib need Continuous number)
//--------------------------------------------------------------------
slider1:0.25<0.0,1.0.0001>BIT 
slider2:0.25<0.0,0.9>FREQ
//====================================================================
in_pin: left input
in_pin: right input
out_pin:left output
out_pin:right output
//====================================================================

@init
//====================================================================

    // create APP ID
    //================================================================
    APP_ID = APP_create();
    //================================================================

    //create Layer ID
    //================================================================
    LAYER_001 = LAYER_create("LAYER01");
    //================================================================

    //create Component and add to layer 1 .
    //================================================================
    TEST_comp  = COMPONENT_create("TEST");
    XYSlider_1 = XYSLIDER_create("XYSlider_1");
    XYSLIDER_setSliderIndexX(XYSlider_1,2);
    XYSLIDER_setSliderIndexY(XYSlider_1,1);
    //----------------------------------------------------------------
    LAYER_addChildComponent(LAYER_001 , TEST_comp);
    LAYER_addChildComponent(LAYER_001 , XYSlider_1);
    //================================================================

    // layer add to app
    //================================================================
    APP_addChildComponent(APP_ID,LAYER_001);
    //================================================================
    LAYER_setActive(LAYER_001,true);
    //================================================================

    // canvase test
    //================================================================
    CANVS_WAVE_L = CANVAS_create(gfx_w,gfx_h);
    CANVS_WAVE_R = CANVAS_create(gfx_w,gfx_h);
    //================================================================
    
    //================================================================
    DBG_toggle(DBG); // visible off
    //================================================================
    
    // for particle 
    PARTICLE_controller = PARTICLE_CONTROLLER_create();
    //================================================================
    
    //bitCrusher params
    //================================================================
    fBits = 1;
    fFreq = 1;
    //================================================================
    _fStep   =0;;
    _fTemp   =0;;
    _fPhase -=1.0/srate;
    _fLastValueL =0;
    _fLastValueR =0;
    //================================================================

@slider
//====================================================================
    XYSLIDER_setYValue(XYSlider_1,slider1,false);
    XYSLIDER_setXValue(XYSlider_1,slider2,false);
    //================================================================
    APP_SliderChanged(APP_ID); // notify for update lastValue
    //================================================================
    
@block
//====================================================================
    max_L = 0;min_L = 0;
    max_R = 0;min_R = 0;
    //================================================================
    
@sample
//====================================================================
    max_L = max(max_L,spl0);
    min_L = min(min_L,spl0);
    max_R = max(max_R,spl1);
    min_R = min(min_R,spl1);
    //================================================================
    fBits = XYSLIDER_getYValue(XYSlider_1);;
    //fFreq =1- XYSLIDER_getYValue(XYSlider_1);;
    fFreq = XYSLIDER_getXValue(XYSlider_1);;
    
    _fPhase -=1.0/srate;
    //================================================================
    (    fFreq == 1.0 
      || _fPhase < 0.) ?
    (
        _fPhase += 1.0/(fFreq*47000.+1000);
        //============================================================
        ( fBits  < 1.0)?
        (
            _fStep = fBits*510+2;
            _fLastValueL=(floor(spl0*_fStep)) /_fStep;
            _fLastValueR=(floor(spl1*_fStep)) /_fStep;
        ):(
            _fLastValueL = spl0;
            _fLastValueR = spl1;
        );
    );
    //================================================================
    spl0 = _fLastValueL;
    spl1 = _fLastValueR;

@gfx 200 400
//====================================================================
    // direct draw background to
    //================================================================
    drawBackGrond_color( 20
                       , 120 + 120 *  ( XYSLIDER_getXValue(XYSlider_1) )
                       , 12*20 *  ( XYSLIDER_getYValue(XYSlider_1) ));
    drawSinWaveCircle(100,0.8,1,0.0043);
    //================================================================

    // canvase test
    //================================================================
    drawWaveBufferToCanvas(CANVS_WAVE_L
                          ,max_L
                          ,min_L
                          ,40 * ( 1- XYSLIDER_getXValue(XYSlider_1) )
                          ,0.3
                          ,1
                          ,80);

    drawWaveBufferToCanvas(CANVS_WAVE_R
                          ,max_R
                          ,min_R
                          ,40 * ( XYSLIDER_getYValue(XYSlider_1) )
                          ,0.4
                          ,1
                          ,0);
    //================================================================
    gfx_a =0.6;
    CANVAS_blitImage4(CANVS_WAVE_R,1,0);
    CANVAS_blitImage4(CANVS_WAVE_L,1,0);
    gfx_a =1;
    //================================================================
    // final background
    //================================================================
    drawDotGrid(16,16,0.6);
    //================================================================
   
    // for debug
    //================================================================
    DBGMESSAGE_drawAllMessage(DBG);
    //================================================================
    
    // for particle 
    PARTICLE_CONTROLLER_update ( PARTICLE_controller );
    //================================================================
    ( APP_isMouseButtonHoldingPress(APP_ID)  ==true) ?      
    (
        PARTICLE_CONTROLLER_addParticleType1(PARTICLE_controller);
        0;
    );
    //================================================================
    (time_precise() -lastPTime_  > 0.25)?
    (
        PARTICLE_CONTROLLER_addParticleType2_XY (PARTICLE_controller
                                                ,gfx_w/2
                                                ,gfx_h/2
                                                ,false
                                               );
       lastPTime_ = time_precise();
    );
    //================================================================

    // App update. check and compare with last event state.(resize or mouse)
    //================================================================
    APP_update(APP_ID);
    //================================================================

    //================================================================
    ( mouse_cap & 64 ==64 && lastCAP != mouse_cap )?
    (
        DBG_toggle(DBG);
    );
    lastCAP = mouse_cap;
    //================================================================
    ( APP_isMouseButtonPressed(APP_ID)  ==true) ?
    ( 0;);
    
    ( APP_isMouseButtonReleased(APP_ID) ==true) ?
    ( 0;);
    //================================================================
    
    
    // alignment position when resized.
    //================================================================
    ( APP_isWindowResized(APP_ID) ==true) ?
    (
        RECTANGLE_copyTo      ( CANVS_WAVE_L,APP_ID);
        RECTANGLE_copyTo      ( CANVS_WAVE_R,APP_ID);
        CANVAS_recreateCanvas ( CANVS_WAVE_L ); 
        CANVAS_recreateCanvas ( CANVS_WAVE_R );
        //============================================================
        RECTANGLE_copyTo      ( LAYER_001,APP_ID);  // layer resize 
        //============================================================
    );
    //================================================================

    //================================================================
    ( LAYER_isAreaResized(LAYER_001) ==true)   ?
    (
        RECTANGLE_copyTo         (TEST_comp,LAYER_001);
        RECTANGLE_cropFromTopP   (TEST_comp,0.025);
        //============================================================
        RECTANGLE_copyTo         (XYSlider_1,LAYER_001);
        RECTANGLE_reduceP        (XYSlider_1,0.1,0.1);
        //============================================================
    );
    //================================================================
    // if animation
    //================================================================
    COMPONENT_setComponentAlpha(TEST_comp,abs(sin($pi*2*mod*2)));
    mod += 0.01;(mod > 1 )?(mod=0;);
    //================================================================
    RECTANGLE_setX( LAYER_001,40*sin($pi*2*mod));
    
    // plugin Title Str
    //================================================================
    RECTANGLE_drawBottomRightText( APP_ID,"kawa Test Bit Crusher");
    //================================================================
    

//====================================================================
// ç¶š

