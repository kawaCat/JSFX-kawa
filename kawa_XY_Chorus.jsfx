//====================================================================
desc:_kawa_XY_Chorus
//====================================================================

// imports
//====================================================================
import kawa_ALL.jsfx-inc
//====================================================================

// sliders //( this app lib need Continuous number)
//--------------------------------------------------------------------
slider1:0.25<0.0,1.0>RATE 
slider2:0.25<0.0,1.0>WIDTH
slider3:0<0,1,{Normal,Deep}> Chorus Mode
slider4:0<0,3,{Sin,Saw,Rect,Tri}> Wave Type
slider5:0<0,100> Smoozing (ms)
slider6:1<0,1>DRY / WET
slider7:0<0,1,{Wave,Modulation}>View
//====================================================================
in_pin: left input
in_pin: right input
out_pin:left output
out_pin:right output
// out_pin: Modulation Signal Out ( for debug ("spl2" is in near line 119. i used Osciloscope(cockos jsfx) for check )
//====================================================================
@init
//====================================================================

    // create APP ID
    //================================================================
    app_ID = APP_create();
    //================================================================

    //create Layer ID
    //================================================================
    layer_001  = LAYER_create("LAYER01");
    //================================================================

    //create Component and add to layer 1 .
    //================================================================
    comp_Border  = COMPONENT_create("TEST");
    comp_XYSlider_1 = XYSLIDER_create("comp_XYSlider_1");
    XYSLIDER_setSliderIndexX(comp_XYSlider_1,1);
    XYSLIDER_setSliderIndexY(comp_XYSlider_1,2);
    //----------------------------------------------------------------
    LAYER_addChildComponent(layer_001  , comp_Border);
    LAYER_addChildComponent(layer_001  , comp_XYSlider_1);
    //================================================================

    // layer add to app
    //================================================================
    APP_addChildComponent(app_ID,layer_001 );
    //================================================================
    LAYER_setActive(layer_001 ,true);
    //================================================================

    // canvase test
    //================================================================
    canvas_WAVE_L = CANVAS_create(gfx_w,gfx_h);
    canvas_WAVE_R = CANVAS_create(gfx_w,gfx_h);
    //================================================================
    
    delaySize = srate;
    DELAY_ARRAY_L = CURSOLBUFFER_create(delaySize);
    DELAY_ARRAY_R = CURSOLBUFFER_create(delaySize);
    DELAY_ARRAY_L_MIXED = CURSOLBUFFER_create(100);
    DELAY_ARRAY_R_MIXED = CURSOLBUFFER_create(100);
    //================================================================
    
    //================================================================
    DBG_toggle(DBG); // visible off
    //================================================================
    
    // for particle 
    PARTICLE_controller = PARTICLE_CONTROLLER_create();
    //================================================================
    waveSignale_ID = WAVESINE_create();
    WAVESINE_setPolarType (waveSignale_ID,DEFINE_WAVE_SIGNALE_UNIPOLAR);
    //================================================================
    
    smooth_coeff = exp(-1/(slider5/1000*srate)); // 4 ms smoothing
    //================================================================
    
    waveSignalForGraph_ID = WAVESINE_create();
    WAVESINE_setPolarType (waveSignalForGraph_ID,DEFINE_WAVE_SIGNALE_UNIPOLAR);
    bufferForGraph = CURSOLBUFFER_create (srate*3);
    bufferForGraphReadPtr = 0;
    //================================================================
@slider
//====================================================================
    XYSLIDER_setXValue(comp_XYSlider_1,slider1,false);
    XYSLIDER_setYValue(comp_XYSlider_1,slider2,false);
    WAVESINE_setGenerateWaveType(waveSignale_ID,slider4);
    WAVESINE_setGenerateWaveType(waveSignalForGraph_ID,slider4);
    //================================================================
    smooth_coeff = exp(-1/(slider5/1000*srate)); 
    //================================================================
    APP_SliderChanged(app_ID); // notify for update lastValue
    //================================================================
    
@block
//====================================================================
    f_max_L = 0;f_min_L = 0;
    f_max_R = 0;f_min_R = 0;
    //================================================================
    
@sample
//====================================================================
    f_max_L = max(f_max_L,spl0);
    f_min_L = min(f_min_L,spl0);
    f_max_R = max(f_max_R,spl1);
    f_min_R = min(f_min_R,spl1);
    //================================================================
    
    //================================================================
    (slider3 ==0)?
    (
        //normal Mode
        rateX= slider1 * 5;//Max 5hz
        //============================================================
        WAVESINE_setRate(waveSignale_ID,rateX);
        newValueX = ( WAVESINE_getAndTick (waveSignale_ID));
        valueX = singlepole(valueX,newValueX,smooth_coeff);
        //============================================================
        chorusWidth = ( srate* 0.0001 )*10* XYSLIDER_getYValue(comp_XYSlider_1) ;
        //============================================================
        WAVESINE_setRate(waveSignalForGraph_ID,rateX);
        valueXForGraph_new = ( WAVESINE_getAndTick (waveSignalForGraph_ID));
        valueXForGraph = singlepole(valueXForGraph,valueXForGraph_new,smooth_coeff);
        //============================================================
        CURSOLBUFFER_push( bufferForGraph ,valueXForGraph );
        //============================================================
    ):(
        //deep Mode
        rateX= slider1 * 1;//Max 12hz
        //============================================================
        WAVESINE_setRate(waveSignale_ID,rateX);
        newValueX = ( WAVESINE_getAndTick (waveSignale_ID));
        valueX = singlepole(valueX,newValueX,smooth_coeff);
        //============================================================
        chorusWidth = ( srate* 0.0001  )*200* XYSLIDER_getYValue(comp_XYSlider_1) ;
        //============================================================
        WAVESINE_setRate(waveSignalForGraph_ID,rateX);
        valueXForGraph_new = ( WAVESINE_getAndTick (waveSignalForGraph_ID));
        valueXForGraph = singlepole(valueXForGraph,valueXForGraph_new,smooth_coeff);
        //============================================================
        CURSOLBUFFER_push( bufferForGraph ,valueXForGraph );
    );
    //================================================================
    
    CURSOLBUFFER_push( DELAY_ARRAY_L ,spl0 );
    CURSOLBUFFER_push( DELAY_ARRAY_R ,spl1 );
    //================================================================
    //  (this process getted with noise in the case of pure sine wave.)
    // delay_L = CURSOLBUFFER_getOffsettedValue( DELAY_ARRAY_L
    //                                         , - chorusWidth * valueX -1 );
    // delay_R = CURSOLBUFFER_getOffsettedValue( DELAY_ARRAY_R
    //                                         , - chorusWidth * valueX -1 );
    //================================================================
   
    //================================================================
    newOffsetSample =  chorusWidth * valueX+1;
    offsetSample = singlepole(offsetSample,newOffsetSample,smooth_coeff);
    //================================================================
    
    // with interpolation ??
    //================================================================
    delay_L = CURSOLBUFFER_getOffsettedValueWithInterp( DELAY_ARRAY_L
                                                      , offsetSample );
    //================================================================
    delay_R = CURSOLBUFFER_getOffsettedValueWithInterp( DELAY_ARRAY_R
                                                      , offsetSample );
    
    CURSOLBUFFER_push( DELAY_ARRAY_L_MIXED , (spl0 + delay_L ) );
    CURSOLBUFFER_push( DELAY_ARRAY_R_MIXED , (spl1 + delay_R ) );
    //================================================================
    wet_L = CURSOLBUFFER_getOffsettedValueWithInterp( DELAY_ARRAY_L_MIXED
                                                      , 40 );
    wet_R = CURSOLBUFFER_getOffsettedValueWithInterp( DELAY_ARRAY_R_MIXED
                                                      , 40 );
    //================================================================
    spl0 = spl0 *(1-slider6) + (wet_L ) * (slider6) ;
    spl1 = spl1 *(1-slider6) + (wet_R ) * (slider6) ;
    //================================================================
    
    //================================================================

@gfx 200 400
//====================================================================
    // direct draw background to
    //================================================================
    drawBackGrond_color(5,0,60);
    drawSinWaveCircle(100,0.8,1,0.0043);
    //================================================================

     // for particle 
    PARTICLE_CONTROLLER_update ( PARTICLE_controller );
 
    (slider7 ==0)?
    (
    
    
        // canvase test
        //================================================================
        drawWaveBufferToCanvas(canvas_WAVE_L
                              ,f_max_L
                              ,f_min_L
                              ,40 * XYSLIDER_getXValue(comp_XYSlider_1)
                              ,0.3
                              ,1
                              ,80);
        //================================================================
        drawWaveBufferToCanvas(canvas_WAVE_R
                              ,f_max_R
                              ,f_min_R
                              ,40 * XYSLIDER_getYValue(comp_XYSlider_1)
                              ,0.4
                              ,1
                              ,0);
        //================================================================
        gfx_a =0.6;
        CANVAS_blitImage4(canvas_WAVE_R,1,0);
        CANVAS_blitImage4(canvas_WAVE_L,1,0);
        gfx_a =1;
    );
    //================================================================
    // final background
    //================================================================
    drawDotGrid(16,16,0.6);
    //================================================================
    
    //================================================================
    (slider7 ==1)?
    (
        bufferNum = CURSOLBUFFER_getMaxSize( bufferForGraph);
        drawPointNum = min(srate/rateX*2,srate);
        xDelta = gfx_w / drawPointNum;
        count_BB = 0;
        lastVV_YY = 0;
        lastVV_XX = gfx_w;
        //============================================================
        while(count_BB < drawPointNum )
        (
            //========================================================
            xx = gfx_w-count_BB *xDelta;
            vv_YY = CURSOLBUFFER_getOffsettedValue(   bufferForGraph
                                              ,  -count_BB );
            //========================================================                               
            yy = -vv_YY * gfx_h/2*0.4 +gfx_h/2;
            //========================================================
            bufferForGraphReadPtr +=1;
            (bufferForGraphReadPtr >= bufferNum)?
            (      
            bufferForGraphReadPtr = 0;  
            );
                   
            gfx_a = 0.1;gfx_r = 1;gfx_g = 1;gfx_b = 1;   
            //========================================================
            gfx_rect(xx,yy,1,gfx_h/2-yy+1,false);

            gfx_a = 0.4;gfx_r = 0;gfx_g = 0;gfx_b = 0;  
            gfx_line(lastVV_XX,lastVV_YY,xx+1,yy,false);
            lastVV_XX = xx;
            lastVV_YY = yy;
            //========================================================
            count_BB += 1;
        );
        gfx_a = 1;
        gfx_rect( 0,gfx_h/2-1, gfx_w, 2,true);
    );
    //================================================================
  
   
   
    // for debug
    //================================================================
    //DBGMESSAGE_drawAllMessage(DBG);
    //================================================================
    
   
    //================================================================
    ( APP_isMouseButtonHoldingPress(app_ID)  ==true) ? 
    (
        PARTICLE_CONTROLLER_addParticleType1(PARTICLE_controller);
        0;
    );
    //================================================================
    (time_precise() -lastPTime_  > 0.25)?
    (
        PARTICLE_CONTROLLER_addParticleType2_XY (PARTICLE_controller
                                                ,gfx_w/2
                                                ,gfx_h/2
                                                ,false
                                               );
        lastPTime_ = time_precise();
    );
    //================================================================

    // App update. check and compare with last event state.(resize or mouse)
    //================================================================
    APP_update(app_ID);
    
    //================================================================
    ( mouse_cap & 64 ==64 && lastCAP != mouse_cap )?
    (
        DBG_toggle(DBG);
    );
    lastCAP = mouse_cap;
    //================================================================
    ( APP_isMouseButtonPressed(app_ID)  ==true) ?
    ( 0;);
    
    ( APP_isMouseButtonReleased(app_ID) ==true) ?
    ( 0;);
    //================================================================
    
    
    // alignment position when resized.
    //================================================================
    ( APP_isWindowResized(app_ID) ==true) ?
    (
        RECTANGLE_copyTo      ( canvas_WAVE_L,app_ID);
        RECTANGLE_copyTo      ( canvas_WAVE_R,app_ID);
        CANVAS_recreateCanvas ( canvas_WAVE_L ); 
        CANVAS_recreateCanvas ( canvas_WAVE_R );
        //============================================================
        RECTANGLE_copyTo      ( layer_001 ,app_ID);  // layer resize 
        //============================================================
    );
    //================================================================

    //================================================================
    ( LAYER_isAreaResized(layer_001 ) ==true)   ?
    (
        RECTANGLE_copyTo         (comp_Border,layer_001 );
        RECTANGLE_cropFromTopP   (comp_Border,0.025);
        //============================================================
        RECTANGLE_copyTo         (comp_XYSlider_1,layer_001 );
        RECTANGLE_reduceP        (comp_XYSlider_1,0.1,0.1);
    );
    //================================================================
    // if animation
    //================================================================
    COMPONENT_setComponentAlpha(comp_Border,abs(sin($pi*2*mod*2)));
    mod += 0.01;(mod > 1 )?(mod=0;);
    //================================================================
    
    // plugin Title Str
    //================================================================
    RECTANGLE_drawBottomRightText( app_ID,"XY Chorus");
    //================================================================

//====================================================================
// ç¶š

